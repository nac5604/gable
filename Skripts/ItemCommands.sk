# Class Name: ItemCommands.sk
# Author: nac#3030
# Deps: skript-YAML, SkBee

# Gives the player a sword with the specified rarity.
command /giveWeapon <text> <text>:
	permission: admin.weapon.give
	trigger:
		give Weapon(arg-1, arg-2) to player
		send "Given weapon &e'%arg-1%&e'&f with rarity &e'%arg-2%&e'&f." to player

# Register a sword in-game.
command /registerWeapon <text> <text> <num>:
	permission: admin.weapon.register
	trigger:
		load yaml "plugins/Skript/scripts/XS-Data/properties/weapons.yaml" as "weapons.yaml"

		if yaml node arg-1 in "swords.yaml" exists:
			send "This entry already exists." to player
		else:
			set {_enchantments::*} to enchantments of player's tool
			Weapon_Register(arg-1, player's tool, arg-2, {_enchantments::*}, arg-3)
			send "Registerd weapon &e'%arg-1%&e'&f with display &e'%colored arg-2%&e'&f." to player
	
		save yaml "weapons.yaml"
		unload yaml "weapons.yaml"

# Register a rarity in-game.
command /registerRarity <text> <text> <num> <text>:
	permission: admin.rarity.register
	trigger:
		
		load yaml "plugins/Skript/scripts/XS-Data/properties/rarities.yaml" as "rarities.yaml"

		if yaml node arg-1 in "rarities.yaml" exists:
			send "This entry already exists." to player
		else:
			Rarity_Register(arg-1, arg-2, arg-3, arg-4)
			send "Registerd rarity &e'%arg-1%&e'&f with display &e'%colored arg-2%&e'&f." to player
		
		save yaml "rarities.yaml"
		unload yaml "rarities.yaml"

# Tab completions for /giveSword.
on tab complete of "/giveWeapon":
	set tab completions for position 1 to GetRootNodes("plugins/Skript/scripts/XS-Data/properties/weapons.yaml")
	set tab completions for position 2 to GetRootNodes("plugins/Skript/scripts/XS-Data/properties/rarities.yaml")

# Tab completions for /registerRarity.
on tab complete of "/registerRarity":
	set tab completions for position 4 to "MULTIPLY" and "ADD"

# Deals the correct amount of damage for the weapon.
on damage:
	if "YAML-Damage" tag of nbt of attacker's tool is set:
		set {_weaponDamage} to "YAML-Damage" tag of nbt of attacker's tool
	
		set damage to {_weaponDamage}

		send action bar "&cðŸ—¡ &4%damage%" to attacker